# scons build file for cMsg tests
Import('env', 'binInstallDir', 'archDir', 'execLibs', 'debugSuffix')

cfileList  = Glob('*.c',  strings=True)
ccfileList = Glob('*.cc', strings=True)

# Compile C++ files and install that executable if "scons install" is run
for fileName in cfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['cmsg'+debugSuffix, 'cmsgRegex'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/regexp/'+archDir, '#src/libsrc/'+archDir],
                               CPPPATH = ['.', '#src/regexp', '#src/libsrc'])
    
    if 'install' in COMMAND_LINE_TARGETS:
        env.Alias('install', env.Install(target = binInstallDir+'/test', source = compiledFile))

# Compile C++ files and install
for fileName in ccfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['cmsgxx'+debugSuffix,
                                          'cmsg'+debugSuffix,
                                          'cmsgRegex'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/regexp/'+archDir,
                                          '#src/libsrc/'+archDir,
                                          '#src/libsrc++/'+archDir],
                               CPPPATH = ['.', '#src/regexp', '#src/libsrc', '#src/libsrc++'])

    if 'install' in COMMAND_LINE_TARGETS:
        env.Alias('install', env.Install(target = binInstallDir+'/test', source = compiledFile))
