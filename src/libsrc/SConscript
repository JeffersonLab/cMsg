# scons build file for cmsg
    
Import('env', 'libInstallDir', 'incInstallDir', 'archIncInstallDir', 'debugSuffix')
import os
from os import sep

headerList = ['cMsg.h',
              'cMsgConstants.h',
              'cMsgDomain.h',
              'cMsgNetwork.h',
              'cMsgPrivate.h']

linkList = [archIncInstallDir + sep + 'cMsg.h',
            archIncInstallDir + sep + 'cMsgConstants.h',
            archIncInstallDir + sep + 'cMsgDomain.h',
            archIncInstallDir + sep + 'cMsgNetwork.h',
            archIncInstallDir + sep + 'cMsgPrivate.h']


fileList = ['cMsg.c',
           'cMsgCompoundPayload.c',
           'cMsgDomain.c',
           'cMsgDomainListenThread.c',
           'cMsgDomainUtil.c',
           'cMsgMatching.c',
           'cMsgCommonNetwork.c',
           'encode.c',
           'fileDomain.c',
           'dummyDomain.c',
           'hash.c',
           'rcDomain.c',
           'rcDomainListenThread.c',
           'rwlock.c',
           'sunConcurrency.c',
           'polarssl_padlock.c',
           'polarssl_aes.c']

docList = ['cMsg.h',
           'cMsgConstants.h',
           'cMsg.c',
           'cMsgCompoundPayload.c']

env.AppendUnique(CPPPATH = ['.', '#src/regexp'])
cmsgLib = env.SharedLibrary(target = 'cmsg'+debugSuffix, source = fileList)
env.Alias("install", env.Install(target = incInstallDir, source = headerList))
env.Alias("install", env.Install(target = libInstallDir, source = cmsgLib))

# create symbolic links to installed header files
if archIncInstallDir != incInstallDir:
    env.Alias("install", env.CreateSymbolicLinks(target = linkList,
        source = env.Install(target = incInstallDir, source = headerList)))

if 'doc' in COMMAND_LINE_TARGETS:
    # generate documentation
    env.Alias('doc', env.DocGenC(target = ['#/doc/doxygen/C/html/index.html'],
                                 source = docList ))
