# scons build file for cmsg

Import('env', 'libDir', 'incDir', 'archIncDir', 'debugSuffix')
import os
from os import sep

headerList = ['cMsg.hxx',
              'cMsgPrivate.hxx']

linkList   = [archIncDir + sep + 'cMsg.hxx',
              archIncDir + sep + 'cMsgPrivate.hxx']

fileList   = ['cMsgWrapper.cc',
              'cMsgPayload.cc']

docList = ['cMsg.hxx',
           'cMsgWrapper.cc',
           'cMsgPayload.cc']              

env.AppendUnique(CPPPATH = ['.', '#src/regexp', '#src/libsrc'])
cmsgxxLib = env.SharedLibrary(target = 'cmsgxx'+debugSuffix, source = fileList)
    
env.Alias("install", env.Install(target = incDir, source = headerList))
env.Alias("install", env.Install(target = libDir, source = cmsgxxLib))

# create symbolic links to installed header files
if archIncDir != incDir:
    env.Alias("install", env.CreateSymbolicLinks(target = linkList,
        source = env.Install(target = incDir, source = headerList)))

if 'doc' in COMMAND_LINE_TARGETS:
    # generate documentation
    env.Alias('doc', env.DocGenCC(target = ['#/doc/doxygen/CC/html/index.html'],
                                  source = docList ))
